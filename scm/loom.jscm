;; -*- mode: scheme; -*-

;; Penelope Project Software Copyright (C) 2020 FoAM Kernow
;;
;; This program is free software: you can redistribute it and/or modify
;; it under the terms of the GNU Affero General Public License as
;; published by the Free Software Foundation, either version 3 of the
;; License, or (at your option) any later version.
;;
;; This program is distributed in the hope that it will be useful,
;; but WITHOUT ANY WARRANTY; without even the implied warranty of
;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;; GNU Affero General Public License for more details.
;;
;; You should have received a copy of the GNU Affero General Public License
;; along with this program.  If not, see <http://www.gnu.org/licenses/>.

(load "flx/scm/fluxus.jscm")
(load "scm/symbolic.jscm")
(load "scm/constructor.jscm")
(load "scm/components.jscm")

(define (make-loom draft warp weft state t root weft-count)
  (list draft warp weft state t root weft-count))

(define (loom-draft l) (list-ref l 0))
(define (loom-modify-draft l v) (list-replace l 0 v))
(define (loom-warp l) (list-ref l 1))
(define (loom-modify-warp l v) (list-replace l 1 v))
(define (loom-weft l) (list-ref l 2))
(define (loom-modify-weft l v) (list-replace l 2 v))
(define (loom-state l) (list-ref l 3))
(define (loom-modify-state l v) (list-replace l 3 v))
(define (loom-t l) (list-ref l 4))
(define (loom-modify-t l v) (list-replace l 4 v))
(define (loom-root l) (list-ref l 5))
(define (loom-weft-count l) (list-ref l 6))
(define (loom-modify-weft-count l v) (list-replace l 6 v))

;; ----------------------------------------------------------

(define (build-loom draft warp-threads weft-threads warp-colours weft-colours)
  (let ((root (build-locator))
		(weft-threads (make-divisible-by-wefts weft-threads (length weft-colours))))
    (with-state
     (shader-assign-compiled "default")  
     (parent root)
     (make-loom
      draft
      (build-list
       warp-threads
       (lambda (i)
		 (constructor
		  (kernel->warp-thread draft i weft-threads)
		  0
		  (mrotate
		   (mtranslate (midentity) (vector (+ i 0.5) 1.5 0))
		   (vector 0 0 1)
		   (* -90 rad-conv))
		  (vmul (list-ref warp-colours (modulo i (length warp-colours))) 2)
		  '())))
      (build-list
       (length weft-colours)
	   (lambda (i)
		 (constructor
		  (kernel->weft-thread
		   draft i warp-threads weft-threads (length weft-colours))
		  i
		  (mtranslate (midentity) (vector i (- i) (* i 0.01))) ;; prevent z fighting
		  (vmul (list-ref weft-colours (modulo i (length weft-colours))) 2)		
		  '())))
      "filling-warp"
      0
      root
	  0))))

(define (loom-step-warp l components)
  (loom-modify-warp
   l (map
      (lambda (e)
		(with-state
		 (parent (loom-root l))
		 (constructor-step e components)))
      (loom-warp l))))

(define (loom-step-weft-all l components)
  (loom-modify-weft
   l (map
      (lambda (e)
		(with-state
		 (parent (loom-root l))
		 (constructor-step e components)))
      (loom-weft l))))

(define (loom-step-weft l components)
  (loom-modify-weft
   l (list-replace
	  (loom-weft l)
	  (loom-weft-count l) 
	  (with-state
	   (parent (loom-root l))
	   (constructor-step
		(list-ref (loom-weft l) (loom-weft-count l))
		components)))))

(define (loom-step-weft-n l components n)
  (if (zero? n)
	  l
	  (loom-step-weft-n 
	   (loom-modify-weft
		l (list-replace
		   (loom-weft l)
		   (loom-weft-count l) 
		   (with-state
			(parent (loom-root l))
			(constructor-step
			 (list-ref (loom-weft l) (loom-weft-count l))
			 components))))
	   components (- n 1))))

(define move-time 1)
(define weave-time 0.05)

;;(define (loom-selvedge-step? step)
;;  (or (eq? step "l") (eq? step "r")
;;	  (eq? step "lu") (eq? step "ru")
;;	  (eq? step "ld") (eq? step "rd")
;;	  (eq? step "dl") (eq? step "dr")
;;	  (eq? step "ul") (eq? step "ur")))

(define (loom-selvedge-step? step)
  (or
   (eq? step "l") (eq? step "r")
   (eq? step "dl") (eq? step "dr")
   (eq? step "ul") (eq? step "ur")))

(define (loom-not-selvedge-step? step)
  (or (eq? step "u") (eq? step "d")
	  (eq? step "fu") (eq? step "fd")))

(define (loom-update l d components)
  (let ((state (loom-state l)))
    (cond
     ((eq? state "filling-warp")
      (if (and (not (null? (loom-warp l)))
			   (< (constructor-pc (car (loom-warp l)))
				  (- (length (constructor-code (car (loom-warp l)))) 1)))
		  (loom-step-warp l components)
		  (loom-modify-state
		   ;; step the warp a few extra times to bring
		   ;; it under the weaving pos
		   (loom-step-warp 
			(loom-step-warp l components)
			components)
		   "filling-weft")))

     ((eq? state "filling-weft")
      (if (< (constructor-pc (car (loom-weft l)))
			 (- (length (constructor-code (car (loom-weft l)))) 1))
		  (loom-step-weft-all l components)
		  (loom-modify-state l "warping")))
	 
	 ((eq? state "warping")
      (if (> (loom-t l) weave-time)		  
		  (loom-modify-t
		   (if (< (loom-weft-count l) (length (loom-weft l)))
			   (loom-modify-weft-count 
				(loom-step-warp l components)
				(+ (loom-weft-count l) 1))			   
			   (loom-modify-weft-count
				(loom-modify-state l "weaving")
				0))    
		   0)
		  (loom-modify-t l (+ (loom-t l) d))))	  

	 ((eq? state "weaving")
      (if (> (loom-t l) weave-time)
		  (loom-step-weft-n 
		   (loom-modify-t
			(let ((step (constructor-next-instr (list-ref (loom-weft l) (loom-weft-count l)))))
			  ;;(msg (list "w" (loom-weft-count l) step (constructor-pc (list-ref (loom-weft l) (loom-weft-count l)))))
			  (if (loom-selvedge-step? step)
				  (if (< (loom-weft-count l) (- (length (loom-weft l)) 1))
					  (loom-modify-weft-count
					   l (+ (loom-weft-count l) 1))
					  (loom-modify-weft-count
					   (loom-modify-state l "selvedge") 0))
				  l))   
			0)
		   components 1)
		  (loom-modify-t l (+ (loom-t l) d))))

	 ((eq? state "selvedge")
      (if (> (loom-t l) weave-time)
		  (loom-step-weft 
		   (loom-modify-t
			(let ((step (constructor-next-instr (list-ref (loom-weft l) (loom-weft-count l)))))
			  ;;(msg (list "s" (loom-weft-count l) step (constructor-pc (list-ref (loom-weft l) (loom-weft-count l)))))
			  ;;(msg (loom-weft l))
			  (if (loom-not-selvedge-step? step)
				  (if (< (loom-weft-count l) (- (length (loom-weft l)) 1))
					  (loom-modify-weft-count
					   l (+ (loom-weft-count l) 1))
					  (loom-modify-weft-count
					   (loom-modify-state l "read") 0))
				  l))   
			0)
		   components)
		  (loom-modify-t l (+ (loom-t l) d))))

	 ((eq? state "read")
	  (msg "read")
	  (loom-modify-state
	   (loom-update-draft 
		l (read-draft-interface)
		(read-number "fabric-warp-size")
		(make-divisible-by-wefts
		 (read-number "fabric-weft-size")
		 (length (loom-weft l))))
	   "moving"))
	 
	 ((eq? state "moving")
      (with-primitive
       (loom-root l)
       (translate (vector 0 (* d (length (loom-weft l))) 0)) ;; one unit per second
       (if (> (loom-t l) move-time)
		   (loom-modify-state l "warping")
		   (loom-modify-t l (+ (loom-t l) d)))))
     (else l))))


(define (loom-update-draft l draft warp-threads weft-threads)
  (loom-modify-draft
   (loom-modify-weft
    (loom-modify-warp
     l (index-map
		(lambda (i e)
		  (constructor-modify-code
		   e (kernel->warp-thread draft i weft-threads)))
		(loom-warp l)))
    (index-map
     (lambda (i e)
       (constructor-modify-code
		e (kernel->weft-thread
		   draft i warp-threads weft-threads
		   (length (loom-weft l)))))
	 (loom-weft l)))
   draft))


